#
# Try to find the petsc library
#

INCLUDE(FindPackageHandleStandardArgs)

FIND_PATH(PETSC_INCLUDE_DIR petscconf.h
  HINTS
    # petsc is special. Account for that
    ${PETSC_DIR}/${PETSC_ARCH}/include
    $ENV{PETSC_DIR}/$ENV{PETSC_ARCH}/include
  PATH_SUFFIXES petsc
)

FIND_LIBRARY(PETSC_LIBRARY
  NAMES petsc
  HINTS
    # petsc is special. Account for that
    ${PETSC_DIR}/${PETSC_ARCH}
    $ENV{PETSC_DIR}/$ENV{PETSC_ARCH}
  PATH_SUFFIXES lib${LIB_SUFFIX} lib64 lib
)

FIND_PACKAGE_HANDLE_STANDARD_ARGS(PETSC DEFAULT_MSG PETSC_LIBRARY PETSC_INCLUDE_DIR)

IF(PETSC_FOUND)

  FILE(STRINGS "${PETSC_INCLUDE_DIR}/petscversion.h" PETSC_MAJOR_STRING
    REGEX "#define.*PETSC_VERSION_MAJOR")
  STRING(REGEX REPLACE "^.*PETSC_VERSION_MAJOR.*([0-9]+).*" "\\1" PETSC_MAJOR "${PETSC_MAJOR_STRING}")

  FILE(STRINGS "${PETSC_INCLUDE_DIR}/petscversion.h" PETSC_MINOR_STRING
    REGEX "#define.*PETSC_VERSION_MINOR")
  STRING(REGEX REPLACE "^.*PETSC_VERSION_MINOR.*([0-9]+).*" "\\1" PETSC_MINOR "${PETSC_MINOR_STRING}")

  FILE(STRINGS "${PETSC_INCLUDE_DIR}/petscversion.h" PETSC_SUBMINOR_STRING
    REGEX "#define.*PETSC_VERSION_SUBMINOR")
  STRING(REGEX REPLACE "^.*PETSC_VERSION_SUBMINOR.*([0-9]+).*" "\\1" PETSC_SUBMINOR "${PETSC_SUBMINOR_STRING}")

  FILE(STRINGS "${PETSC_INCLUDE_DIR}/petscversion.h" PETSC_RELEASE_STRING
    REGEX "#define.*PETSC_VERSION_RELEASE.*1")
  IF("${PETSC_RELEASE_STRING}" STREQUAL "")
    SET(PETSC_RELEASE FALSE)
  ELSE()
    SET(PETSC_RELEASE TRUE)
  ENDIF()

  SET(PETSC_VERSION "${PETSC_MAJOR}.${PETSC_MINOR}.${PETSC_SUBMINOR}")

  MARK_AS_ADVANCED(
    PETSC_LIBRARY
    PETSC_INCLUDE_DIR
  )
ENDIF()

