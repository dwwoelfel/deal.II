#####
##
## Copyright (C) 2012 by the deal.II authors
##
## This file is part of the deal.II library.
##
## <TODO: Full License information>
## This file is dual licensed under QPL 1.0 and LGPL 2.1 or any later
## version of the LGPL license.
##
## Author: Matthias Maier <matthias.maier@iwr.uni-heidelberg.de>
##
#####

#
# This file implements the DEAL_II_INVOKE_AUTOPILOT macro, which is
# part of the deal.II library.
#
# Usage:
#       DEAL_II_INVOKE_AUTOPILOT()
#
# where it is assumed that the following variables are defined:
#
#       TARGET         -  a string used for the project and target name
#       TARGET_SRC     -  a list of source file to compile for target
#                         ${TARGET}
#       TARGET_RUN     -  (optional) the command line that should be
#                         invoked by "make run", will be set to default
#                         values if empty
#       CLEAN_UP_FILES -  (optional) a list of files (globs) that will be
#                         removed with "make runclean" and "make
#                         distclean", will be set to default values if
#                         empty
#

MACRO(DEAL_II_INVOKE_AUTOPILOT)

  # Define and setup a compilation target:
  ADD_EXECUTABLE(${TARGET} ${TARGET_SRC})
  DEAL_II_SETUP_TARGET(${TARGET})

  IF(NOT "${CMAKE_SOURCE_DIR}" STREQUAL "${CMAKE_BINARY_DIR}")

    MESSAGE(STATUS "Out-of-source build. Target ${TARGET} defined but additional autopilot funcionality is not available.")

  ELSE()

    MESSAGE(STATUS "Autopilot invoked")

    # Define a custom target to easily run the program:
    IF("${TARGET_RUN}" STREQUAL "")
      SET(TARGET_RUN ${TARGET})
    ENDIF()
    ADD_CUSTOM_TARGET(run
      COMMAND ${TARGET_RUN}
      DEPENDS ${TARGET}
      COMMENT "Run ${TARGET} with ${CMAKE_BUILD_TYPE} configuration"
      )

    # Define custom targets to easily switch the build type:
    ADD_CUSTOM_TARGET(debug
      COMMAND ${CMAKE_COMMAND} -DCMAKE_BUILD_TYPE=Debug ${CMAKE_SOURCE_DIR}
      COMMENT "Switch CMAKE_BUILD_TYPE to Debug"
      )
    ADD_CUSTOM_TARGET(release
      COMMAND ${CMAKE_COMMAND} -DCMAKE_BUILD_TYPE=Release ${CMAKE_SOURCE_DIR}
      COMMENT "Switch CMAKE_BUILD_TYPE to Release"
      )

    # And another custom target to clean up all files generated by the program:
    IF("${CLEAN_UP_FILES}" STREQUAL "")
      SET(CLEAN_UP_FILES *.log *.gmv *.gnuplot *.gpl *.eps *.pov *.vtk *.ucd *.d2)
    ENDIF()
    ADD_CUSTOM_TARGET(runclean
      COMMAND ${CMAKE_COMMAND} -E remove ${CLEAN_UP_FILES}
      COMMENT "runclean invoked"
      )

    # Define a distclean target to remove every generated file:
    ADD_CUSTOM_TARGET(distclean
      COMMAND ${CMAKE_COMMAND} --build ${CMAKE_BINARY_DIR} --target clean
      COMMAND ${CMAKE_COMMAND} --build ${CMAKE_BINARY_DIR} --target runclean
      COMMAND ${CMAKE_COMMAND} -E remove_directory CMakeFiles
      COMMAND ${CMAKE_COMMAND} -E remove
        cmake_install.cmake CMakeCache.txt Makefile
      COMMENT "distclean invoked"
      )

    # Define a strip-comments target:
    ADD_CUSTOM_TARGET(strip-comments
      COMMAND perl -pi -e 's\#^[ \\t]*//.*\\n\#\#g;' ${TARGET_SRC}
      COMMENT "strip comments"
      )

    # Print out some usage information:
    MESSAGE(
"###
#
#  Successfully set up project  ${TARGET}  with  ${DEAL_II_PACKAGE_NAME}-${DEAL_II_PACKAGE_VERSION}  found at
#      ${DEAL_II_PATH}
#
#  CMAKE_BUILD_TYPE:   ${CMAKE_BUILD_TYPE}
#  TARGET_SRC:         ${TARGET_SRC}
#
#  You can now run
#      $ make            - to compile and link the program
#      $ make run        - to (compile, link and) run the program
#
#      $ make debug      - to switch the build type to \"Debug\"
#      $ make release    - to switch the build type to \"Release\"
#      $ make edit_cache - to change (cached) configuration variables
#                          and rerun the configure and generate phases of CMake
#
#      $ make clean      - to remove the generated executable as well as
#                          all intermediate compilation files
#      $ make runclean   - to remove all output generated by the program
#      $ make distclean  - to clean the directory from _all_ generated
#                          files (includes clean, runclean and the removal
#                          of the generated build system)
#
#  Have a nice day!
#
###"
      )
  ENDIF()

ENDMACRO()

