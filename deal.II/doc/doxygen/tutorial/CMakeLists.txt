#####
##
## Copyright (C) 2012 by the deal.II authors
##
## This file is part of the deal.II library.
##
## <TODO: Full License information>
## This file is dual licensed under QPL 1.0 and LGPL 2.1 or any later
## version of the LGPL license.
##
## Author: Matthias Maier <matthias.maier@iwr.uni-heidelberg.de>
##
#####


#
# Prepare steps.png and steps.cmapx:
#

file(GLOB _steps
  ${CMAKE_SOURCE_DIR}/examples/step-*
  )

ADD_CUSTOM_COMMAND(
  OUTPUT ${CMAKE_CURRENT_BINARY_DIR}/steps.dot
  COMMAND ${PERL_EXECUTABLE}
  ARGS
    ${CMAKE_SOURCE_DIR}/doc/doxygen/scripts/steps.pl
    ${_steps}
    > ${CMAKE_CURRENT_BINARY_DIR}/steps.dot
  )

ADD_CUSTOM_COMMAND(
  OUTPUT
    ${CMAKE_CURRENT_BINARY_DIR}/steps.png
    ${CMAKE_CURRENT_BINARY_DIR}/steps.cmapx
  COMMAND ${DOXYGEN_DOT_EXECUTABLE}
  ARGS
    -Tpng -o ${CMAKE_CURRENT_BINARY_DIR}/steps.png
    -Tcmapx -o ${CMAKE_CURRENT_BINARY_DIR}/steps.cmapx
    ${CMAKE_CURRENT_BINARY_DIR}/steps.dot
  DEPENDS
    ${CMAKE_CURRENT_BINARY_DIR}/steps.dot
  )


#
# Prepare toc.html:
#

CONFIGURE_FILE(
  ${CMAKE_CURRENT_SOURCE_DIR}/toc.html.in
  ${CMAKE_CURRENT_BINARY_DIR}/toc.html.in
  COPYONLY
  )

ADD_CUSTOM_COMMAND(
  OUTPUT ${CMAKE_CURRENT_BINARY_DIR}/toc.html
  COMMAND ${PERL_EXECUTABLE}
  ARGS
    ${CMAKE_SOURCE_DIR}/doc/doxygen/scripts/make_toc.pl
    > ${CMAKE_CURRENT_BINARY_DIR}/toc.html
  WORKING_DIRECTORY
    ${CMAKE_CURRENT_BINARY_DIR}
  DEPENDS
    ${CMAKE_CURRENT_BINARY_DIR}/toc.html.in
    ${CMAKE_CURRENT_BINARY_DIR}/steps.cmapx
  )


#
# TODO:
# Are toc-list.html and toc-topics.html really generated by hand?
#


#
# A target for the preparation of all the stuff happening in here...
#
ADD_CUSTOM_TARGET(tutorial
  DEPENDS
    ${CMAKE_CURRENT_BINARY_DIR}/toc.html
  )


#
# Prepare the steps for documentation generation
#

FOREACH(_step ${_steps})
  GET_FILENAME_COMPONENT(_step "${_step}" NAME)
  STRING(REGEX REPLACE "-" "_" _step_underscore "${_step}")

  CONFIGURE_FILE(
    ${CMAKE_CURRENT_SOURCE_DIR}/_toc.intro.snippet.in
    ${CMAKE_CURRENT_BINARY_DIR}/${_step}_toc.intro.snippet
    )

  CONFIGURE_FILE(
    ${CMAKE_CURRENT_SOURCE_DIR}/_plain.snippet.in
    ${CMAKE_CURRENT_BINARY_DIR}/${_step}_plain.snippet
    )

  ADD_CUSTOM_COMMAND(
    OUTPUT ${CMAKE_CURRENT_BINARY_DIR}/${_step}.cc
    COMMAND ${PERL_EXECUTABLE}
    ARGS
      ${CMAKE_SOURCE_DIR}/doc/doxygen/scripts/program2plain
      < ${CMAKE_SOURCE_DIR}/examples/${_step}/${_step}.cc
      >> ${CMAKE_CURRENT_BINARY_DIR}/${_step}.cc
    DEPENDS
      ${CMAKE_SOURCE_DIR}/examples/${_step}/${_step}.cc
    VERBATIM
    )

  ADD_CUSTOM_COMMAND(
    #
    # This has to be refactored really, really badly...
    #
    OUTPUT ${CMAKE_CURRENT_BINARY_DIR}/${_step}.h
    COMMAND cat
    ARGS
      ${CMAKE_CURRENT_BINARY_DIR}/${_step}_toc.intro.snippet
      >> ${CMAKE_CURRENT_BINARY_DIR}/${_step}.h
    COMMAND ${PERL_EXECUTABLE}
    ARGS
      ${CMAKE_SOURCE_DIR}/doc/doxygen/scripts/intro2toc
      < ${CMAKE_SOURCE_DIR}/examples/${_step}/doc/intro.dox
      >> ${CMAKE_CURRENT_BINARY_DIR}/${_step}.h
    COMMAND cat
    ARGS
      ${CMAKE_CURRENT_SOURCE_DIR}/_toc.prog.snippet
      >> ${CMAKE_CURRENT_BINARY_DIR}/${_step}.h
    COMMAND ${PERL_EXECUTABLE}
    ARGS
      ${CMAKE_SOURCE_DIR}/doc/doxygen/scripts/program2toc
      < ${CMAKE_SOURCE_DIR}/examples/${_step}/${_step}.cc
      >> ${CMAKE_CURRENT_BINARY_DIR}/${_step}.h
    COMMAND cat
    ARGS
      ${CMAKE_CURRENT_SOURCE_DIR}/_toc.results.snippet
      >> ${CMAKE_CURRENT_BINARY_DIR}/${_step}.h
    COMMAND ${PERL_EXECUTABLE}
    ARGS
      ${CMAKE_SOURCE_DIR}/doc/doxygen/scripts/intro2toc
      < ${CMAKE_SOURCE_DIR}/examples/${_step}/doc/results.dox
      >> ${CMAKE_CURRENT_BINARY_DIR}/${_step}.h
    COMMAND cat
    ARGS
      ${CMAKE_CURRENT_SOURCE_DIR}/_toc.final.snippet
      >> ${CMAKE_CURRENT_BINARY_DIR}/${_step}.h
    COMMAND ${PERL_EXECUTABLE}
    ARGS
      ${CMAKE_SOURCE_DIR}/doc/doxygen/scripts/create_anchors
      < ${CMAKE_SOURCE_DIR}/examples/${_step}/doc/intro.dox
      >> ${CMAKE_CURRENT_BINARY_DIR}/${_step}.h
    COMMAND ${PERL_EXECUTABLE}
    ARGS
      ${CMAKE_SOURCE_DIR}/doc/doxygen/scripts/program2doxygen
      < ${CMAKE_SOURCE_DIR}/examples/${_step}/${_step}.cc
      >> ${CMAKE_CURRENT_BINARY_DIR}/${_step}.h
    COMMAND ${PERL_EXECUTABLE}
    ARGS
      ${CMAKE_SOURCE_DIR}/doc/doxygen/scripts/create_anchors
      < ${CMAKE_SOURCE_DIR}/examples/${_step}/doc/results.dox
      >> ${CMAKE_CURRENT_BINARY_DIR}/${_step}.h
    COMMAND cat
    ARGS
      ${CMAKE_CURRENT_BINARY_DIR}/${_step}_plain.snippet
      >> ${CMAKE_CURRENT_BINARY_DIR}/${_step}.h
    WORKING_DIRECTORY
      ${CMAKE_CURRENT_BINARY_DIR}
    DEPENDS
      ${CMAKE_SOURCE_DIR}/examples/${_step}/${_step}.cc
      ${CMAKE_SOURCE_DIR}/examples/${_step}/doc/intro.dox
      ${CMAKE_SOURCE_DIR}/examples/${_step}/doc/results.dox
    VERBATIM
    )
  ADD_CUSTOM_TARGET(tutorial_${_step}
    DEPENDS
      ${CMAKE_CURRENT_BINARY_DIR}/${_step}.h
      ${CMAKE_CURRENT_BINARY_DIR}/${_step}.cc
    )
  ADD_DEPENDENCIES(tutorial tutorial_${_step})
ENDFOREACH()


#
# Install all bits:
#

INSTALL(FILES
  index.html
  navbar.html
  title.html
  toc-list.html
  toc-topics.html
  ${CMAKE_CURRENT_BINARY_DIR}/steps.png
  ${CMAKE_CURRENT_BINARY_DIR}/toc.html
  DESTINATION ${DEAL_II_DOCUMENTATION_RELDIR}/doxygen/tutorial
  COMPONENT documentation
  )

